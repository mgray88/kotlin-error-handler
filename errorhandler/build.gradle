buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray"
    id 'org.jetbrains.kotlin.jvm' version "$kotlin_version"
}

group 'isdigital.errorhandler'
version '1.0.0'

apply plugin: 'java-library'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile "org.mockito:mockito-core:1.+"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'isdigital.errorhandler'
            artifactId 'kotlin-error-handler'
            version '1.0.0'

            from components.java

            artifact sourceJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }

    repositories {
        maven {
            url "./dist"
        }
    }
}


Properties localProps = new Properties()

try {
    localProps.load(project.file('../local.properties').newDataInputStream())
} catch(Exception ex) {
    logger.warn('local.properties file is missing')
}

bintray {
    user = localProps.getProperty('bintrayUser')
    key = localProps.getProperty('bintrayApiKey')
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'kotlin-error-handler'
        desc = 'Error handling library for Android and Kotlin'
        userOrg = "is-digital"
        licenses = ['MIT']
        vcsUrl = 'https://github.com/mgray88/kotlin-error-handler'
        labels = ['kotlin', 'error handler', 'errors', 'android']
        publicDownloadNumbers = true
        version {
            name = '1.0.0'

            desc = 'Error handling library for Android and Kotlin'
            vcsTag = '1.0.0'
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
            }
        }
    }
}
